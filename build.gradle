// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext.globalVersion="1.1.5-SNAPSHOT"
// ext.globalVersion="1.1.5-TEST"
// ext.globalVersion="1.1.5"

ext.githubUserId="k3b"
ext.githubRepository="k3b-geoHelper"

ext.isReleaseVersion = !globalVersion.endsWith("SNAPSHOT") && !globalVersion.endsWith("TEST")

if (!hasProperty("uploadRepository")) {
	ext.uploadRepository = (isReleaseVersion) ? "https://oss.sonatype.org/service/local/staging/deploy/maven2/" : "https://oss.sonatype.org/content/repositories/snapshots/"

	if (!hasProperty("signing.keyId") || !hasProperty("signing.password") || !hasProperty("signing.secretKeyRingFile")) {
		ext.uploadRepository = "file:./build/testrepository/"
		print "signing.keyId=... or signing.password=... or signing.secretKeyRingFile=.. not found in gradle.properties\n"
		print "Cannot upload to 'maven@https://oss.sonatype.org/...' . Using repository '${uploadRepository}' instead \n"
	}
	
	if (!hasProperty("ossrhUsername") || !hasProperty("ossrhPassword")) {
		ext.uploadRepository = "file:./build/testrepository/"
		print "ossrhUsername=... or ossrhPassword=.. not found in gradle.properties\n"
		print "Cannot upload to 'maven@https://oss.sonatype.org/...' . Using '${uploadRepository}' instead \n"
	}
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

allprojects {
    repositories {
        jcenter()
        // maven { url "https://jitpack.io" } // 
    }

	// http://stackoverflow.com/questions/28450703/how-to-set-the-language-level-in-gradle-so-it-is-ide-agnostic/35827609#35827609
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
    } 
}
